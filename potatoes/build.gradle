dependencies {
    compile "com.badlogicgames.gdx:gdx:$gdxVersion"
    compile "com.badlogicgames.gdx:gdx-box2d:$gdxVersion"
    compile "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
    compile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    compile "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-desktop"

    compile "com.badlogicgames.gdx:gdx-backend-headless:$gdxVersion"
    compile "com.badlogicgames.gdx:gdx:$gdxVersion"
    compile 'com.esotericsoftware:kryo:4.0.1'

    testCompile "org.mockito:mockito-all:1.9.+"
    testCompile "com.badlogicgames.gdx:gdx-backend-headless:$gdxVersion"
    testCompile "com.badlogicgames.gdx:gdx:$gdxVersion"
    testCompile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
}

configurations.all {
    // check for updates every build
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}


apply plugin: 'org.flywaydb.flyway'
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath group: 'org.flywaydb', name: 'flyway-gradle-plugin', version: '3.2.1'
    }
    

}


task(run, dependsOn: "classes", type: JavaExec) {
    main = "com.deco2800.potatoes.GameLauncher"
    classpath = sourceSets.main.runtimeClasspath
}

task copyRust(type: Copy) {
    if (System.properties['os.name'].toLowerCase().contains('windows')) {
        from 'target\\release'
        into 'build\\classes\\main'
        include 'rustyfish.dll'
    } else {
        from 'target/release/'
        into "build/classes/main"
        include 'librustyfish.*'
    }
}

task compileRust(type: Exec) {
    if (System.properties['os.name'].toLowerCase().contains('windows')) {
        executable "cmd.exe"
        args "/c", "cargo build --release"
        ignoreExitValue true
    } else {
        executable "sh"
        args "-c", "which cargo >/dev/null || exit 0; cargo build --release"
    }

    finalizedBy "copyRust"
}

run.dependsOn compileRust
build.dependsOn compileRust

task cleanCopiedRust(type: Delete) {
    delete "build/classes/main"
}


task cleanRust(type: Exec) {
    if (System.properties['os.name'].toLowerCase().contains('windows')) {
        executable "cmd.exe"
        args "/c", "cargo clean"
        ignoreExitValue true
    } else {
        executable "sh"
        args "-c", "which cargo >/dev/null || exit 0; cargo clean"
    }

    finalizedBy cleanCopiedRust
}

clean.dependsOn cleanRust

task testRust(type: Exec) {
    if (System.properties['os.name'].toLowerCase().contains('windows')) {
        executable "cmd.exe"
        args "/c", "cargo test"
        ignoreExitValue true
    } else {
        executable "sh"
        args "-c", "which cargo >/dev/null || exit 0; cargo test"
    }
}

test.dependsOn testRust
